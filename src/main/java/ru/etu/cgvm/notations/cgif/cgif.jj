options {
    SANITY_CHECK = true;
    DEBUG_PARSER = true;
 }

PARSER_BEGIN(CgifParser)

package ru.etu.cgvm.notations.cgif.parser;

import ru.etu.cgvm.objects.Arc;
import ru.etu.cgvm.objects.Referent;
import ru.etu.cgvm.objects.SignatureParameter;
import ru.etu.cgvm.objects.Type;
import ru.etu.cgvm.objects.base.Graph;
import ru.etu.cgvm.objects.graphs.Context;
import ru.etu.cgvm.objects.graphs.Lambda;
import ru.etu.cgvm.objects.nodes.Actor;
import ru.etu.cgvm.objects.nodes.Concept;
import ru.etu.cgvm.objects.nodes.Relation;

import java.io.ByteArrayInputStream;
import java.io.InputStream;
import java.io.Reader;
import java.nio.charset.StandardCharsets;
import java.util.Deque;
import java.util.LinkedList;

public class CgifParser {

     private Context topContext;

     public CgifParser() {
         this((Reader) null);
     }

     public Context parse(String content) throws ParseException {
            InputStream is = new ByteArrayInputStream(content.getBytes());
            ReInit(is, StandardCharsets.UTF_8.name());
            topContext = new Context();
            initTopLevelGraph();
            return topContext;
     }
}
PARSER_END(CgifParser)

SKIP : {
  " "
| "\t"
| "\n"
| "\r"
}

// Если попадается /* в состоянии DEFAULT, пропускаем и переключаемся в состояние IN_MULTI_LINE_COMMENT
MORE : {
  "/*" : IN_MULTI_LINE_COMMENT
}

// В состоянии IN_MULTI_LINE_COMMENT пропускаем все символы
<IN_MULTI_LINE_COMMENT> MORE : {
    < ~[] >
}

// Если в состоянии IN_MULTI_LINE_COMMENT попадается */ пропускаем его и снова переключаемся в режим DEFAULT
<IN_MULTI_LINE_COMMENT> SPECIAL_TOKEN : {
     "*/" : DEFAULT
}

TOKEN : {
    <LBRACE: "{">
|   <RBRACE: "}">
|   <LPAREN: "(">
|   <RPAREN: ")">
|   <LBRACKET: "[">
|   <RBRACKET: "]">
|   <COLON: ":">
|   <QUESTIONMARK: "?">
|   <STAR: "*">
|   <COMMA: ",">
|   <AMP: "&">
|   <ATSIGN: "@">
|   <POUNDSIGN: "#">
|   <PERCENTSIGN: "%">
|   <LESSTHAN: "<">
|   <GREATERTHAN: ">">
|   <EQUAL: "=">
|   <BAR: "|">
|   <DOUBLEQUOTE: "\"">
|   <SINGLEQUOTE: "'">
|   <UNDERSCORE: "_">
|   <TILDE: "~">
|   <TYPEHIERARCHY: "TypeHierarchy">
|   <RELATIONHIERARCHY: "RelationHierarchy">
|   <TYPELABEL: "TypeLabel">
|   <RELATIONLABEL: "RelationLabel">
|   <LAMBDAEXPRESSION: "LambdaExpression">
|   <DEF : "Def">
|   <LAMBDA : "lambda">
|   <ORDER : "EQ" | "GT" | "LT">
}

TOKEN : {
/* LITERALS */
   < DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"] >
|   < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|   <QUOTED_STRING: // Текст
        <DOUBLEQUOTE>
        ("\\" ~[] | ~["\"","\\"])*
        <DOUBLEQUOTE>>
|   <CHARACTER_LITERAL: // Name
        <SINGLEQUOTE>
        ("\\" ~["n", "r"] | ~["\"","\\", "\'"])*
        <SINGLEQUOTE>>
/* REFERENT */
|   <ADDITIONAL_INFO: <PERCENTSIGN><IDENTIFIER>>
|   <QUANTIFIER:<ATSIGN>(((<LESSTHAN>|<GREATERTHAN>|<EQUAL>)?<DECIMAL_LITERAL>)|<IDENTIFIER>)>
|   <#SET_ELEMENTS: ((<RELATED_MARK> | <IDENTIFIER>)(<COMMA>(<RELATED_MARK> | <IDENTIFIER>))*) | <STAR>>
|   <STRUCTURE: <LBRACE><SET_ELEMENTS><RBRACE> | <LESSTHAN><SET_ELEMENTS><GREATERTHAN>>
/* IDENTIFIERS */
|   <#DIGIT: ["0"-"9"]>
|   <#LETTER: ["A"-"Z", "a"-"z"]>
|   <IDENTIFIER:  <LETTER> (<LETTER> | <DIGIT> | <UNDERSCORE> )* >
/* MARK */
|   <RELATED_MARK: <QUESTIONMARK><IDENTIFIER>>
|   <DEFINING_MARK: <STAR><IDENTIFIER>>
}

/* Root production. */
void initTopLevelGraph(): {} {
    graph(null)
}

void graph(Graph enclosingGraph): {
    Context graph;
    if (enclosingGraph == null) {
        graph = topContext;
    } else {
        graph = new Context();
        enclosingGraph.addObject(graph);
    }
} {
    (term(graph))+
}

void term(Graph g): {} {
    LOOKAHEAD(3) concept(g) | relation(g) | actor(g) | LOOKAHEAD(3) context(g) | LOOKAHEAD(3) conceptTypeHierarchy(g) | relationTypeHierarchy(g)
}

Referent.Designation designation(): {
    Referent.Designation designation = new Referent.Designation();
    Token element = null;
    Token additionalInfo;
} {
    ([additionalInfo = <ADDITIONAL_INFO>
    {
        designation.setAdditionalInfo(additionalInfo.image);
    }
    ] (element = <DECIMAL_LITERAL> | element = <FLOATING_POINT_LITERAL> | element = <QUOTED_STRING>)
    {
        designation.setLiteral(element.image);
    }
  | (element = <CHARACTER_LITERAL> | element = <IDENTIFIER> | element = <POUNDSIGN>(<DECIMAL_LITERAL>|<IDENTIFIER>))
    {
      designation.setLocator(element.image);
    }
  | element = <QUANTIFIER>
    {
        designation.setQuantifier(element.image);
    })
    {
        return designation;
    }
}

Referent.Descriptor descriptor(): {
    Referent.Descriptor descriptor = new Referent.Descriptor();
    Token additionalInfo;
    Token structure;
} {
    [additionalInfo = <ADDITIONAL_INFO>
    {
        descriptor.setAdditionalInfo(additionalInfo.image);
    }
    ] (structure = <STRUCTURE>)
    {
        descriptor.setStructure(structure.image);
        return descriptor;
    }
}

Referent referent(): {
    Referent referent = new Referent();
    Referent.Descriptor descriptor = null;
    Referent.Designation designation = null;
} {
     <COLON> [LOOKAHEAD(2) descriptor = descriptor()] [designation = designation()]
    {
        referent.setDescriptor(descriptor);
        referent.setDesignation(designation);
        return referent;
    }
  | <TILDE>
    {
        referent.setNegated(true);
        return referent;
    }
}

Concept concept(Graph enclosingGraph): {
    Concept concept = new Concept();
    Type type = null;
    Referent referent = null;
    Token coreferenceLink;
} {
   <LBRACKET> [LOOKAHEAD(2) type = type()]
   [(coreferenceLink = <RELATED_MARK>
   {
        concept.addCoreferenceLink(coreferenceLink.image);
   }
   )+ | coreferenceLink = <DEFINING_MARK>
   {
        concept.addCoreferenceLink(coreferenceLink.image);
   }]
   [referent = referent()] <RBRACKET>
   {
        concept.setType(type);
        concept.setReferent(referent);
        enclosingGraph.addObject(concept);
        return concept;
   }
}

Arc arc(Graph g): {
    Arc arc = new Arc();
    Context context; // concept or context
    Concept concept;
    Token coreferenceLink;
} {
    (LOOKAHEAD(3) concept = concept(g)
    {
        arc.setConcept(concept);
    }| LOOKAHEAD(3) context = context(g)
    {
        arc.setContext(context);
    }| coreferenceLink = <RELATED_MARK>
    {
        arc.setCoreferenceLink(coreferenceLink.image);
    })
    {
        return arc;
    }
}

void relation(Graph enclosingGraph): {
    Relation relation = new Relation();
    Type type;
    Arc arc;
    Deque<Arc> arcs = new LinkedList<>();
} {
    <LPAREN> type = type() (
    arc = arc(enclosingGraph)
    {
        arcs.add(arc);
    }
    )+
    <RPAREN>
    {
        relation.setType(type);
        relation.setOutput(arcs.pollLast()); // n-ая дуга исходящая, оставшиеся — входящие
        relation.addInputArcs(arcs);
        enclosingGraph.addObject(relation);
    }
}

void actor(Graph g): {
    Actor actor = new Actor();
    Type type;
    Arc arc;
} {
    <LESSTHAN> type = type() (arc = arc(g)
    {
        actor.addInputArc(arc);
    }
    )+ <BAR>
    (arc = arc(g)
    {
        actor.addOutputArc(arc);
    }
    )+
    <GREATERTHAN>
    {
        actor.setType(type);
        g.addObject(actor);
    }
}

Lambda lambda(Graph enclosingGraph): {
    SignatureParameter signatureParameter;
    Lambda lambda = new Lambda();
} {
    <LPAREN><LAMBDA><LPAREN>[signatureParameter = parameter()
    {
        lambda.addSignatureParameter(signatureParameter);
    }
    (<COMMA> signatureParameter = parameter()
    {
        lambda.addSignatureParameter(signatureParameter);
    }
    )*]<RPAREN> (term(lambda))+<RPAREN>
    {
        return lambda; // Не добавляем в граф, так как используется в иерархии
    }
}

SignatureParameter parameter(): {
    SignatureParameter signatureParameter = new SignatureParameter();
    Type type;
    Token variable;
} {
    type = type() [variable = <DEFINING_MARK>
    {
        signatureParameter.setVariable(variable.image);
    }]
    {
        signatureParameter.setType(type);
        return signatureParameter;
    }
}

Type type(): {
    Type type = new Type();
    Token name;
    Token negation;
} {
    [negation = <TILDE>
    {
        type.setNegated(negation.image);
    }
    ] name = <IDENTIFIER>
    {
        type.setName(name.image);
        return type;
    }
}

Context context(Graph enclosingGraph): {
    Context context = new Context();
    Token contextName;
} {
   ( <TILDE>
   {
       context.setNegated(true);
   }
   <LBRACKET> (term(context))+<RBRACKET>
  |  <LBRACKET> contextName = <IDENTIFIER>
    {
        context.setName(contextName.image);
    } (<COLON>)? (term(context))+ <RBRACKET>)
    {
        enclosingGraph.addObject(context);
        return context;
    }
}

void conceptTypeHierarchy(Graph g): {} {
    <LBRACKET> <TYPEHIERARCHY> <COLON>
    (LOOKAHEAD(2) conceptTypeDefinition(g) | conceptTypeOrder(g))+
    <RBRACKET>
}

void relationTypeHierarchy(Graph g): {} {
    <LBRACKET> <RELATIONHIERARCHY> <COLON>
    (LOOKAHEAD(2) relationTypeDefinition(g) | relationTypeOrder(g))+
    <RBRACKET>
}

void conceptTypeDefinition(Graph g): {
    Lambda definition;
    String typeName;
} {
    <LPAREN> <DEF>
    typeName = conceptTypeLabel()
    definition = lambdaExpression(g)
    <RPAREN>
    {
        g.getTypeHierarchy().addTypeDefinition(typeName, definition);
    }
}

void relationTypeDefinition(Graph g): {
    Lambda definition;
    String typeName;
} {
    <LPAREN> <DEF>
    typeName = relationTypeLabel()
    definition = lambdaExpression(g)
    <RPAREN>
    {
        g.getTypeHierarchy().addTypeDefinition(typeName, definition);
    }
}

String conceptTypeLabel(): {
    Token typeName;
} {
    <LBRACKET> <TYPELABEL> <COLON> (typeName = <IDENTIFIER>) <RBRACKET>
    {
        return typeName.image;
    }
}

String relationTypeLabel(): {
    Token typeName;
} {
    <LBRACKET> <RELATIONLABEL> <COLON> (typeName = <IDENTIFIER>) <RBRACKET>
    {
        return typeName.image;
    }
}

Lambda lambdaExpression(Graph g): {
    Lambda definition;
} {
    <LBRACKET> <LAMBDAEXPRESSION> <COLON> (definition = lambda(g)) <RBRACKET>
    {
        return definition;
    }
}

void conceptTypeOrder(Graph g): {
    String firstType;
    String secondType;
    Token order;
} {
    <LPAREN>
    order = <ORDER>
    firstType = conceptTypeLabel()
    secondType = conceptTypeLabel()
    <RPAREN>
    {
        g.getTypeHierarchy().addTypeOrder(firstType, secondType, order.image);
    }
}

void relationTypeOrder(Graph g): {
    String firstType;
    String secondType;
    Token order;
} {
    <LPAREN>
    order = <ORDER>
    firstType = relationTypeLabel()
    secondType = relationTypeLabel()
    <RPAREN>
    {
        g.getTypeHierarchy().addTypeOrder(firstType, secondType, order.image);
    }
}